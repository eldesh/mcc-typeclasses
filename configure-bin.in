dnl $Id: configure-bin.in 3019 2010-12-20 08:58:21Z wlux $
dnl
dnl Copyright (c) 2002-2008, Wolfgang Lux
dnl

AC_INIT([%PACKAGE_NAME% for %TARGET%],[%VERSION%],[wlux@uni-muenster.de])
AC_PREREQ(2.50)
AC_CONFIG_SRCDIR(lib/curry.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_RANLIB
AC_PROG_LN_S
AC_PROG_INSTALL
INSTALL_DIR='${INSTALL: -c=} -d -m 755'

dnl Include additional compiler options for Gnu C
if test "$GCC" = yes; then
  CURRY_C_DYNAMIC_NO_PIC
  CURRY_CFLAGS="%CURRY_CFLAGS%"
fi

dnl Check for Cygpath
HC_PATH_STYLE="%HC_PATH_STYLE%"
if test "$HC_PATH_STYLE" = windows; then
  AC_PATH_PROG(CYGPATH, cygpath)
fi

dnl Checks for libraries.
AC_SEARCH_LIBS(sin, lm)
AC_SEARCH_LIBS(inet_addr, nsl)
AC_SEARCH_LIBS(socket, socket)

dnl Check for GNU MP.
AC_CHECK_LIB(gmp, __gmpz_init,
    	     [LIBS="-lgmp $LIBS" have_libgmp=yes],
    	     [have_libgmp=no])
AC_CHECK_HEADER([gmp.h], [have_gmp_h=yes], [have_gmp_h=no])
if test $have_gmp_h = no || test $have_libgmp = no; then
  AC_MSG_ERROR([Gnu MP library not found.  If it is installed in a
non-standard location be sure to invoke configure with the correct
settings for CPPFLAGS and LDFLAGS.  If not installed you can download
the Gnu MP library from http://www.swox.com/gmp/])
fi

dnl Check for a shell, which is suitable for cyi
CURRY_CYI_SHELL

AC_SUBST(CURRY_CFLAGS)
AC_SUBST(INSTALL_DIR)
AC_SUBST(HC_PATH_STYLE)
AC_SUBST_FILE(in_place_config)
in_place_config=$srcdir/in-place-config

AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([bin/cyc], [chmod +x bin/cyc])
AC_CONFIG_FILES([bin/cymake], [chmod +x bin/cymake])
AC_CONFIG_FILES([bin/cyi], [chmod +x bin/cyi])
AC_OUTPUT
